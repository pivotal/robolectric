import org.gradle.internal.jvm.Jvm

new RoboJavaModulePlugin(
        deploy: true,
).apply(project)

// Disable annotation processor for tests
compileTestJava {
    options.compilerArgs.add("-proc:none")
}

test {
    enabled = false
}

dependencies {
    // Project dependencies
    compile project(":annotations")

    // Compile dependencies
    compile "com.google.errorprone:error_prone_annotation:$errorproneVersion"
    compile "com.google.errorprone:error_prone_refaster:$errorproneVersion"
    compile "com.google.errorprone:error_prone_check_api:$errorproneVersion"
    compile "com.google.auto.service:auto-service:1.0-rc4"
    compileOnly(AndroidSdk.MAX_SDK.coordinates) { force = true }

    annotationProcessor "com.google.auto.service:auto-service:1.0-rc4"

    // in jdk 9, tools.jar disappears!
    def toolsJar = Jvm.current().getToolsJar()
    if (toolsJar != null) {
        compile files(toolsJar)
    }

    // Testing dependencies
    testCompile "junit:junit:4.12"
    testCompile "com.google.truth:truth:0.42"
    testCompile("com.google.errorprone:error_prone_test_helpers:$errorproneVersion") {
        exclude group: 'junit', module: 'junit' // because it depends on a snapshot!?
    }
}
